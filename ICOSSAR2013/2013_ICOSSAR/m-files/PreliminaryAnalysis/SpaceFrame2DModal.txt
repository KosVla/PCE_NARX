/CLEAR
/CWD 'D:\SpaceFrame\'
/CONFIG,NRES,10000

*SET,NoExp,50

! =======================================================
! PREPROCESSOR
! =======================================================
/PREP7
! Material 1: Vertical beams 
ET,1,BEAM188
MP,EX,1,200E9
MP,PRXY,1,0.29
MP,DENS,1,7850


! Cross-section 1 (vertical elements)
SECTYPE,1,BEAM,RECT
SECDATA,0.30,0.30,3,3
! Cross-section 2 (vertical elements)
SECTYPE,2,BEAM,RECT
SECDATA,0.25,0.25,3,3
! Cross-section 3 (vertical elements)
SECTYPE,3,BEAM,RECT
SECDATA,0.20,0.20,3,3
! Cross-section 4 (vertical elements)
SECTYPE,4,BEAM,RECT
SECDATA,0.15,0.15,3,3
! Cross-section 5 (vertical elements)
SECTYPE,5,BEAM,RECT
SECDATA,0.10,0.10,3,3
! Cross-section 6 (horizontal outer elements)
SECTYPE,6,BEAM,RECT
SECDATA,0.10,0.10,3,3
SECCONTROLS,,,,6524

! Keypoints (front profile)
! Ground floor
K,1, 0.0, 0.0, 0.0
K,2, 4.0, 0.0, 0.0
! 1st floor
K,3, 0.0, 0.0, 3.0
K,4, 4.0, 0.0, 3.0
! 2nd floor
K,5, 0.0, 0.0, 6.0
K,6, 4.0, 0.0, 6.0
! 3rd floor
K,7, 0.0, 0.0, 9.0
K,8, 4.0, 0.0, 9.0
! 4th floor
K,9, 0.0, 0.0, 12.0
K,10, 4.0, 0.0, 12.0
! 5th floor
K,11, 0.0, 0.0, 15.0
K,12, 4.0, 0.0, 15.0

! Lines
! TYPE,1
! Vertical Beams b/w grounf-1st floor
TYPE,1
L,1,3	! Element 1
L,2,4	! Element 2
! Vertical Beams b/w 1st floor-2nd floor
L,3,5	! Element 3
L,4,6	! Element 4
! Vertical Beams b/w 2nd floor-3rd floor
L,5,7	! Element 5
L,6,8	! Element 6
! Vertical Beams b/w 3rd floor-4th floor
L,7,9	! Element 7
L,8,10	! Element 8
! Vertical Beams b/w 4th floor-5th floor
L,9,11	! Element 9
L,10,12	! Element 10

! TYPE,2
! Horizontal Beams 1st floor (outer beams)
L,3,4	! Element 11
L,5,6	! Element 12
L,7,8	! Element 13
L,9,10	! Element 14
L,11,12	! Element 15

! Meshing 
LESIZE,ALL,,,1,1,1  					! Specifies the divisions and spacing ratio on unmeshed lines (no division, no spacing ratio)
! Verical beams (ground-1st floor) 
LATT,1,1,1,,,,1
LSEL,S,LINE,,1,2,1,1
LMESH,ALL								! Generates nodes and line elements along lines
ALLSEL,ALL  
! Verical beams (1st-2nd floor) 
LATT,1,1,1,,,,2
LSEL,S,LINE,,3,4,1,1
LMESH,ALL								! Generates nodes and line elements along lines
ALLSEL,ALL  
! Verical beams (2nd-3rd floor) 
LATT,1,1,1,,,,3
LSEL,S,LINE,,5,6,1,1
LMESH,ALL								! Generates nodes and line elements along lines
ALLSEL,ALL  
! Verical beams (3rd-4th floor) 
LATT,1,1,1,,,,4
LSEL,S,LINE,,7,8,1,1
LMESH,ALL								! Generates nodes and line elements along lines
ALLSEL,ALL  
! Verical beams (4th-5th floor) 
LATT,1,1,1,,,,5
LSEL,S,LINE,,9,10,1,1
LMESH,ALL								! Generates nodes and line elements along lines
ALLSEL,ALL  
! Horizontal inner beams 
LATT,1,1,1,,,,6
LSEL,S,LINE,,11,15,1,1
LMESH,ALL								! Generates nodes and line elements along lines
ALLSEL,ALL

FINISH


! Read the Material Properties Input file
! =======================================                                                                                                                                      
*DIM,BeamProps,ARRAY,NoExp,2 
*VREAD,BeamProps(1),'BeamProps','txt',,IJK,NoExp,2
(E16.7)                                                                                                                                                              

/DELETE,'MProps',txt,
/DELETE,'EProps',txt,
/DELETE,'Wn',txt,

*DO,J,1,NoExp			! Experiments DO LOOP
	/PREP7
	MPDELE,ALL,1,1,1					! Delete material properties
	! Material 1: Vertical beams 
	MP,EX,1,BeamProps(J,1)          	! N/m^2	
	MP,PRXY,1,0.29
	MP,DENS,1,7850

	! Cross-section 1 (vertical elements)
	SECTYPE,1,BEAM,RECT
	SECDATA,0.30*BeamProps(J,2),0.30*BeamProps(J,2),3,3
	! Cross-section 2 (vertical elements)
	SECTYPE,2,BEAM,RECT
	SECDATA,0.25*BeamProps(J,2),0.25*BeamProps(J,2),3,3
	! Cross-section 3 (vertical elements)
	SECTYPE,3,BEAM,RECT
	SECDATA,0.20*BeamProps(J,2),0.20*BeamProps(J,2),3,3
	! Cross-section 4 (vertical elements)
	SECTYPE,4,BEAM,RECT
	SECDATA,0.15*BeamProps(J,2),0.15*BeamProps(J,2),3,3
	! Cross-section 5 (vertical elements)
	SECTYPE,5,BEAM,RECT
	SECDATA,0.10*BeamProps(J,2),0.10*BeamProps(J,2),3,3

	FINISH	
	
	! Write material properties for the current experiment
	*CFOPEN,'MProps',txt,,APPEND			! Open txt file
	*GET,MEx,EX,1							! Material 1 Young moduli
	*GET,BEAMheight1,SECP,1,DATA,1			! Beam 1 cross section area
	*GET,BEAMheight2,SECP,2,DATA,1			! Beam 2 cross section area
	*GET,BEAMheight3,SECP,3,DATA,1			! Beam 3 cross section area
	*GET,BEAMheight4,SECP,4,DATA,1			! Beam 4 cross section area
	*GET,BEAMheight5,SECP,5,DATA,1			! Beam 5 cross section area
	*GET,BEAMheight6,SECP,6,DATA,1			! Beam 4 cross section area
	*VWRITE,J,MEx,BEAMheight1,BEAMheight2,BEAMheight3,BEAMheight4,BEAMheight5,BEAMheight6
(8(E16.7,2X))
	*CFCLOS	

	*CFOPEN,'EProps',txt,,APPEND					! Open txt file
	*GET,Emat,ELEM,1,ATTR,MAT						! Element 1 material
	*GET,Etype,ELEM,1,ATTR,TYPE						! Element 1 type
	*GET,Esecn1,ELEM,1,ATTR,SECN					! 1 section
	*GET,Esecn2,ELEM,3,ATTR,SECN					! 2 section
	*GET,Esecn3,ELEM,5,ATTR,SECN					! 3 section
	*GET,Esecn4,ELEM,7,ATTR,SECN					! 4 section
	*GET,Esecn5,ELEM,9,ATTR,SECN					! 5 section
	*GET,Esecn6,ELEM,11,ATTR,SECN					! 6 section
	*VWRITE,J,Emat,Etype,Esecn1,Esecn2,Esecn3,Esecn4,Esecn5,Esecn6
(9(F3.0,2X))
	*CFCLOS
	
	! =======================================================
	! SOLUTION 
	! =======================================================
	/SOLU                 
	ANTYPE,MODAL 					! Specifies the analysis type
	MODOPT,LANB,9,0,200,,OFF		! Specifies modal analysis options                                                                                                                                                        
	EQSLV,SPAR          			! Specifies the type of equation solver                                                                                                                                                   
	MXPAND,0, , ,0      			! Number of modes to expand                                                                                                                                                   
	LUMPM,0 						! Lumped mass matrix formulation (on/off)                                                                                                                                                            
	PSTRES,0            			! Prestress effects included (on/off)                                                                                                                                                   
	
	! Rayleigh Damping
	! ================
	ALPHAD,2.0
	BETAD,0.0001
	
	! Boundary conditions
	! ===============
	NSEL,S,LOC,Z,0
	D,ALL,ALL
	ALLSEL,ALL
	
	*CFOPEN,'Wn',txt,,APPEND
	
	SOLVE       		! Starts a solution                                                                                                                                                           
	*GET,WN1,MODE,1,FREQ                                                                                                                                          
	*GET,WN2,MODE,2,FREQ                                                                                                                                               
	*GET,WN3,MODE,3,FREQ                                                                                                                                               
	*GET,WN4,MODE,4,FREQ                                                                                                                                               
	*GET,WN5,MODE,5,FREQ                                                                                                                                                                                                                                                                                        
	*GET,ZETA1,MODE,1,DAMP
	*GET,ZETA2,MODE,2,DAMP
	*GET,ZETA3,MODE,3,DAMP
	*GET,ZETA4,MODE,4,DAMP
	*GET,ZETA5,MODE,5,DAMP
	*VWRITE,WN1,ZETA1,WN2,ZETA2,WN3,ZETA3,WN4,ZETA4,WN5,ZETA5
(10(F15.10,1X,))                                                                                                                                                       
	*CFCLOS

*ENDDO
FINISH